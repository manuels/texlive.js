LHSName  "ArabTeX-Kurdish"
RHSName  "UNICODE"

Define a U+0061  ;a
Define b U+0062  ;b
Define c U+0063  ;c
Define d U+0064  ;d
Define e U+0065  ;e
Define f U+0066  ;f
Define g U+0067  ;g
Define h U+0068  ;h
Define i U+0069  ;i
Define j U+006A  ;j
Define k U+006B  ;k
Define l U+006C  ;l
Define m U+006D  ;m
Define n U+006E  ;n
Define o U+006F  ;o
Define p U+0070  ;p
Define q U+0071  ;q
Define r U+0072  ;r
Define s U+0073  ;s
Define t U+0074  ;t
Define u U+0075  ;u
Define v U+0076  ;v
Define w U+0077  ;w
Define x U+0078  ;x
Define y U+0079  ;y
Define z U+007A  ;z
Define A U+0041  ;A
Define B U+0042  ;B
Define E U+0045  ;E
Define G U+0047  ;G
Define I U+0049  ;I
Define N U+004E  ;N
Define O U+004F  ;O
Define T U+0054  ;T
Define U U+0055  ;U
Define Y U+0059  ;Y
Define C U+005E  ;^  << CARET
Define UL U+005F ;_  << UNDERLINE 
Define P U+002E  ;.  << DOT
Define V U+002C  ;,  << COMMA (_V_irgule)
Define CO U+003A ;:  << COLON
Define Ain U+0060 ;` 
Define H U+0027  ;'  << HAMZA 
Define Q U+0022  ;"  << QUOTE
Define Z U+002D  ;-  << HYPHEN
Define BAR U+007C ;|
Define LLAH U+006C U+006C U+005F U+0061 U+0068 

pass(Unicode)

Class [Digits] = (U+0030..U+0039)
Class [ArDigits] = (U+0660..U+0669)


; special ligatures
;; 1. Standard ArabTeX input
;; WARNING! there are, alas, two classes of Arabic fonts:
;; a) Those that are Unicode-compliant and include the initial alif in glyph U+FDF2 
;;    For these the ligature llah can be accessed by the sequence lam-lam-ha
;; b) Those that do not have the initial alif in U+FDF2
(a|A) l Z LLAH > U+FDF2 ;; 'al-ll_ah' or 'Al-ll_ah'
LLAH > U+0644 U+0644 U+0647  ;; 'll_ah' -> 'l l h'
;; UPPERCASE mnemonics for various ligatures or special glyphs
;; 2a. custom ArabXeTeX input for ligatures not defined in Unicode
U+004C U+004C U+0048 > U+0644 U+0644 U+0647  ;; 'LLH' -> 'l l h'
U+0046 U+0041 U+004C U+0049 U+004C U+004C U+0041 U+0048 > U+0641 U+064E U+0644 U+0650 U+0644 U+0647 ;; 'FALILLAH' : ligature in Adobe Arabic
;; 2b. Ligatures and glyphs defined in Unicode:
U+0041 U+004C U+004C U+0041 U+0048 > U+FDF2 ; 'ALLAH'
U+004C U+004C U+0041 U+0048 > U+0644 U+0644 U+0647 ; 'LLAH'

U+0053 U+004C U+004D > U+FDFA ; 'SLM' : .sallY al-ll_ah `alayhi wa-sallam
U+0028 U+0028 > U+FD3F ;; '((' : ornate right par
U+0029 U+0029 > U+FD3E ;; '))' : ornate left par
U+0052 U+0049 U+0059 U+0041 U+004C > U+FDFC ;; 'RIYAL' : Saudi currency sign
;; most fonts won't have the following ones:
U+0053 U+0041 U+004C U+004C U+0041 U+0053 U+0054 U+004F U+0050 > U+FDF0 ;; 'SALLASTOP' (.sallY with "ye-barree", Quranic stop sign)
U+0051 U+0041 U+004C U+0041 > U+FDF1 ;; 'QALA' : qalY with ye-barree, Quranic stop sign
U+0041 U+004B U+0042 U+0041 U+0052 > U+FDF3 ;; 'AKBAR'
U+004D U+0055 U+0048 U+0041 U+004D U+004D U+0041 U+0044 > U+FDF4 ;; 'MUHAMMAD'
U+0053 U+0041 U+004C U+0041 U+004D > U+FDF5 ;; 'SALAM'
U+0052 U+0041 U+0053 U+004F ? U+0055 U+004C > U+FDF6 ;; 'RASUL' (also unicode 'RASOUL)'
U+0041 U+004C U+0041 U+0059 U+0048 (U+0049|U+0045) > U+FDF7 ;; 'ALAYHI' (also unicode 'ALAYHE)'
U+0057 U+0041 U+0053 U+0041 U+004C U+004C U+0041 U+004D > U+FDF8 ;; 'WASALLAM' 
U+0053 U+0041 U+004C U+004C U+0041 > U+FDF9 ;; 'SALLA'
U+004A U+0041 U+004C U+004C U+0041 > U+FDFB ;; 'JALLA' : "jalla jalAluhu"
U+0042 U+0041 U+0053 U+004D U+0041 U+004C U+0041 > U+FDFD ;; 'BASMALA' : very few fonts have this ligature afaik

U+005E U+0053 U+004C U+0059 > U+06D6 ;; ^SLY
U+005E U+0051 U+004C U+0059 > U+06D7 ;; ^QLY
U+005E U+004D U+0049 U+004D > U+06D8 ;; ^MIM-
U+005E U+004C U+0041 > U+06D9 ;; ^LA
U+005E U+004A U+0049 U+004D > U+06DA ;; ^JIM
U+005E U+0044 U+004F U+0054 U+0053 > U+06DB ;; ^DOTS
U+005E U+0053 U+0049 U+004E > U+06DC ;; ^SIN
U+0048 U+0049 U+005A U+0042 > U+06DE ;; HIZB
U+0043 U+0049 U+0052 U+0043 U+005A U+0045 U+0052 U+004F > U+06DF ;; CIRCZERO
U+0052 U+0045 U+0043 U+0054 U+005A U+0045 U+0052 U+004F > U+06E0 ;; RECTZERO
U+005E U+004A U+0041 U+005A U+004D > U+06E1 ;; ^JAZM
U+005E U+004D U+0049 U+004D > U+06E2 ;; ^MIM
U+005F U+0053 U+0049 U+004E > U+06E3 ;; _SIN
U+005E U+004D U+0041 U+0044 U+0044 U+0041 > U+06E4 ;; ^MADDA
U+0057 U+0041 U+0057 > U+06E5 ;; WAW
U+0059 U+0045 U+0048 > U+06E6 ;; YEH
U+005E U+0059 U+0045 U+0048 > U+06E7 ;; ^YEH
U+005E U+004E U+0055 U+004E > U+06E8 ;; ^NUN
U+0053 U+0041 U+004A U+0044 U+0041 > U+06E9 ;; SAJDA
U+005F U+0053 U+0054 U+004F U+0050 > U+06EA ;; _STOP
U+005E U+0053 U+0054 U+004F U+0050 > U+06EB ;; ^STOP
U+005E U+0052 U+0053 U+0054 U+004F U+0050 > U+06EC ;; ^RSTOP
U+005F U+004D U+0049 U+004D > U+06ED  ;; _MIM

U+0044 U+004F U+0054 U+0053 > U+061E ;; DOTS  (also possible by typing "::")

;; This is to enclose digits within glyph U+06DD, see http://scripts.sil.org/cms/scripts/page.php?site_id=nrsi&item_id=ArabicFonts
U+005B U+005B ([Digits]+)=dig U+005D U+005D > U+202E U+06DD @dig U+202C ; [[digits]]


; vowels

E / # _  > U+0626 U+06CE
E > U+06CE
e / # _  > U+0626 U+0647 U+200C ; h always final
e > U+0647 U+200C
i / # _ > U+0626 
Q i > U+0650 
i > ;
Q I / # _ > U+0626 U+064A U+0650
Q I > U+064A U+0650
I / # _ > U+0626 U+064A 
I > U+064A
(a|A) / # _ > U+0626 U+0627
(a|A) > U+0627 
(o|O) / # _ > U+0626 U+06C6
(o|O) > U+06C6
u / # _ > U+0626 U+0648
u > U+0648
U / # _ > U+0626 U+0648 U+0648
U > U+0648 U+0648
Ain e > U+0639 U+0647 U+200C


P z P z	  > U+0638 U+0651  ;;SHADDA
P z	  > U+0638 
C z C z	  > U+0698 U+0651  ;;SHADDA
C z	  > U+0698 
V z V z   > U+0696 U+0651 ;pashto ;;SHADDA
V z       > U+0696 ;pashto
z z	  > U+0632 U+0651  ;;SHADDA
z	  > U+0632 
Y Y	  > U+0649 U+0651  ;;SHADDA
Y	  > U+0649 
w w	  > U+0648 U+0651 ;;SHADDA
w 	  > U+0648
y y	  > U+064A U+0651  ;;SHADDA
y	  > U+064A 
v v	  > U+06A4 U+0651  ;;SHADDA
v	  > U+06A4 
V t V t   > U+0679 U+0651 ; urdu ;;SHADDA
V t 	  > U+0679 
UL t UL t > U+062B U+0651  ;;SHADDA
UL t	  > U+062B 
P t P t	  > U+0637 U+0651  ;;SHADDA
P t	  > U+0637 
t t	  > U+062A U+0651  ;;SHADDA
t	  > U+062A 
T T	  > U+0629 U+0651  ;;SHADDA
T	  > U+0629 
(C|V) s (C|V) s   > U+0634 U+0651  ;;SHADDA
(C|V) s	  > U+0634 
P s P s	  > U+0635 U+0651  ;;SHADDA
P s	  > U+0635 
CO s CO s > U+069B U+0651
CO s      > U+069B ; sin with 3 dots below
s s	  > U+0633 U+0651  ;;SHADDA
s	  > U+0633 
;; also missing U+0694 : r with dot below 
V r V r   > U+0694 U+0651 ;Kurdish ;;SHADDA 
V r       > U+0694
C r C r	  > U+0692 U+0651 ;Kurdish ;;SHADDA ; missing in ArabTeX  <<<
C r	  > U+0692 
P r P r	  > U+0695 U+0651 ;Kurdish ;;SHADDA
P r	  > U+0695 
r r	  > U+0631 U+0651  ;;SHADDA
r	  > U+0631 
P q P q	  > U+066F U+0651  ;;SHADDA
P q	  > U+066F 
q q	  > U+0642 U+0651  ;;SHADDA
q	  > U+0642 
p p	  > U+067E U+0651  ;;SHADDA
p	  > U+067E 
C n C n	  > U+06AD U+0651  ;;SHADDA
C n	  > U+06AD 
P n P n   > U+06BA U+0651  ; urdu ;;SHADDA
P n 	  > U+06BA 	   ;
V n V n   > U+06BC U+0651 ; Pashto
V n       > U+06BC ; Pashto
n n	  > U+0646 U+0651  ;;SHADDA
n	  > U+0646 
m m	  > U+0645 U+0651  ;;SHADDA
m	  > U+0645 
P l P l	  > U+06B6 U+0651 ; 
P l 	  > U+06B6 ; ADDED <<< 
C l C l	  > U+06B5 U+0651 ;kurdish ;;SHADDA
C l	  > U+06B5
l l	  > U+0644 U+0651  ;;SHADDA
l	  > U+0644 
P k P k > U+06A9 U+0651 ; pashto urdu ;;SHADDA 
P k > U+06A9
UL k UL k > U+063A U+0651 ;;SHADDA
UL k  > U+063A
k k	  > U+0643 U+0651  ;;SHADDA
k	  > U+0643 
c c       > U+062C U+0651  ;;SHADDA
c	  > U+062C 
x x	  > U+062E U+0651  ;;SHADDA
x	  > U+062E 
j j	  > U+0698 U+0651  ;;SHADDA
j	  > U+0698 
UL h UL h > U+062E U+0651  ;;SHADDA
UL h	  > U+062E 
P h P h	  > U+062D U+0651  ;;SHADDA
P h	  > U+062D 
V h V h   > U+06C1 U+0651
V h       > U+06C1
h h	  > U+200C U+06BE U+0651  ;;SHADDA
h	  > U+200C U+06BE ; h always initial form
V g V g > U+06AC U+0651 ;;SHADDA
V g > U+06AC ; kaf with dot above
P (g|x) P (g|x) > U+063A U+0651  ;;SHADDA
P (g|x)	  > U+063A 
C g C g	  > U+063A U+0651  ;;SHADDA
C g	  > U+063A 
g g	  > U+06AF U+0651  ;;SHADDA
g	  > U+06AF 
G G       > U+06AB U+0651  ;pashto ;;SHADDA
G         > U+06AB ;pashto
P f P f	  > U+06A1 U+0651  ;;SHADDA
P f	  > U+06A1 
f f	  > U+0641 U+0651  ;;SHADDA
f	  > U+0641 
V d V d  > U+0688 U+0651 ;urdu ;;SHADDA
V d	  > U+0688 ;urdu
C d C d > U+06EE ; not defined in ArabTeX U+0651 ;;SHADDA
C d > U+06EE ; not defined in ArabTeX
UL d UL d > U+0630 U+0651  ;;SHADDA
UL d	  > U+0630 
P d P d	  > U+0636 U+0651  ;;SHADDA
P d	  > U+0636 
d d	  > U+062F U+0651  ;;SHADDA
d	  > U+062F 
(C|V) c (C|V) c	  > U+0686 U+0651  ;;SHADDA
(C|V) c	  > U+0686
P b P b   > U+066E U+0651  ;;SHADDA
P b       > U+066E 
b b	  > U+0628 U+0651  ;;SHADDA
b	  > U+0628 
Ain Ain	  > U+0639 U+0651  ;;SHADDA
Ain	  > U+0639 
H	  > U+0626 
U+002C / [Digits] _ [Digits] > U+002C  ; comma in numerical context not an Arabic comma
Z Z / [Digits] _ [Digits] > U+2013  ; two hyphens in numerical context -> endash
(Z|U+005D|U+005B|U+0028|U+0029)=xx / [Digits] _ [Digits] > U+202D @xx U+202C ; hyphen or brackets in numerical context: surrounded by LRO & PDF marks
[Digits]  > [ArDigits]
Z Z Z / # _ #    > U+2014 
Z Z / # _ #    > U+2013 
Z Z       > U+0640
B         > U+0640
Z / # _   > U+200D  ; -x  > force initial form with "zero-width joiner"
Z / _ #   > U+200D  ;  x- > force final form    
U+003C U+003C  > U+00BB ;<<
U+003E U+003E  > U+00AB ;>>
U+00AB    > U+00BB 
U+00BB    > U+00AB
UL        > U+0640  ;_
U+003F    > U+061F  ;?
U+003B    > U+061B  ;;
U+003A U+003A > U+061E ;; '::' ligature 'DOTS' also defined above...
U+002C    > U+060C  ;,
U+005D / . _ .   > U+200D U+005D U+200D ; keep shaping when inserting ...[...]...
U+005B / . _ .   > U+200D U+005B U+200D ; ... (but of course this screws up contextual analysis, so quoting of hamzas etc may be necessary)
Q	  >  ;
N         >   ;
C         >   ;^
Z      >   ;-
BAR       >   ;| 


